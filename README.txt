
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлелементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлелементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлелементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлелементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ЗаказСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Документ.Заказ.СписокНоменклатуры КАК ЗаказСписокНоменклатуры
		|ГДЕ
		|	ЗаказСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Номенклатура КАК Номенклатура,
		|	ВТ.Количество КАК Количество,
		|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТ.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ВТ КАК ВТ)) КАК ОстаткиНоменклатурыОстатки
		|		ПО ВТ.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОстаткиНоменклатурыОстатки.Партия.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Нехватка = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2", ВыборкаНоменклатура.Номенклатура, Нехватка);
			Сообщение.Сообщить();
				
		КонецЕсли;
		
		Если Отказ Тогда			
			Продолжить;	
		КонецЕсли;
		
		КоличествоСписать = ВыборкаНоменклатура.Количество;
	
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
			
			Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
				Стоимость = ВыборкаДетальныеЗаписи.СтоимостьОстаток;
			Иначе
				Стоимость = Количество / ВыборкаДетальныеЗаписи.КоличествоОстаток * ВыборкаДетальныеЗаписи.СтоимостьОстаток;
			КонецЕсли;
			
			Движения.ОстаткиНоменклатуры.Записывать = Истина;
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
			Движение.Количество = Количество;
			Движение.Стоимость = Стоимость;
			
			КоличествоСписать = КоличествоСписать - Количество;
			
			Если КоличествоСписать <= 0 Тогда 
				Прервать;	
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
